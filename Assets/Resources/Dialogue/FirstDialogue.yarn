title: Start
tags:
position: 22,-240
---
Hermie: Hello. Who are you?
Friendly Alien: No English yet, eh? Youse must be an F1. //#line:SB1s
Friendly Alien: Well lucky for you stranger, I got a translator right here, courtesy of our lost human folk! //#line:SB1l
Friendly Alien: Feel free to take it with ya! No one speaks Faction 1 anymore, ya know. //#line:SB2l
Buck: Name's Buck, by the way. So anyway adventurer, what d’ya need? //#line:SB3l
    -> What’re you doing here?
        Buck: That’s my line, stranger. This here’s our territory, didn’t ya know?
        Buck: A Faction 1 like you shouldn’t be sneaking ‘round less you got some valuables on hand.
            -> Sorry, I've got nothing.
                Buck: Well, ain’t that a problem?...
                Buck: Tell ya what, I’ve been tryna renovate- Get my quarters in tip-top shape, ya hear?
                Buck: If you could grab me 5 pieces of wood, I’ll give ya some honey. Seems like you could use it. Everyone does.
                Hermie: And where're those?
                Buck: Well, that’s yer job, now ain’t it? C’mon, shouldn’t be too bad if you put yer min’ to it!
                <<jump Interruption1>>
            -> My ship needs repair. Would you happen to have any honey?
                <<jump NeedRepair>>
    -> My ship needs repair. Would you happen to have any honey?
        <<jump NeedRepair>>
         
           
===

title: NeedRepair
position: -138,8
---
Buck: Sure I do.
Hermie: Well? I’m in a rush.
Buck: Woah, hold yer ears, friend! I got some, but it ain’t gon’ be cheap.
Hermie: What do you need?
Buck: Something to rebuild this here place.
Buck: Think you could grab 5 pieces of wood? Should be able to get some if ya put yer min’ to it!
<<jump Interruption1>>
===

title: Interruption1
position: 204,22
---
Alien Officer: So what’d they say? Don’t got a translator on me.
Hermie: They told me to go home and get some sleep.
Alien Officer: Did they now? That's some good advice. Everyone back home ought to like it.
Hermie: ...
Hermie: ...They told me to get wood.
Alien Officer: Then you’ll need to find a way to carry it all! Take a look around.
<<setDialogueNode DialogueManager SouthernGuyQuestPoint Southern_During>>
<<setDialogueNode DialogueManager SleepyQuestPoint LazyDialogue>>
<<StartQuest DialogueManager CraftBagQuest>>
<<SetSlotItems CraftingRecipeUI Quilt Belt Backpack>>
===

title: Southern_During
---
<<if areNumItemsInInventory("Log", 5) == true>>
    <<jump Start_Correct>>
<<else>>
    <<jump Start_Incorrect>>
<<endif>>
===
title: Start_Repeat
---
Buck: Think you could grab 5 pieces of wood? Should be able to get some if ya put yer min’ to it!
===

title: Start_Incorrect
---
Buck: The hell is this, stranger? What, 5 pieces of wood too hard for ya? No wood, no honey, I’m afraid.
===

title: Start_Correct
---
<<removeFromInventory InventoryCanvas Log>>
<<removeFromInventory InventoryCanvas Log>>
<<removeFromInventory InventoryCanvas Log>>
<<removeFromInventory InventoryCanvas Log>>
<<removeFromInventory InventoryCanvas Log>> 
<<addToInventory InventoryCanvas Honey>>
Buck: That’s my boy. I’m an alien of my word. Here y’go!
Hermie: Thanks.
Buck: Don’t bother. Just stay safe out there. Aliens of all kinds tryna start a ruckus causa that so-called “honey war”.
Buck: They don’t take kindly to folks like yerself, regardless of any “truce” we may go pretendin’ we got.
Buck: So, jus’, good luck out there, stranger, for whatever you’re up to.
Buck: heard there's more honey down the well... Kinda dangerous though i hear. 
<<StartQuest DialogueManager GetMoreHoneyQuest>>
===